(*
    module  : nextseq.joy
    version : 1.3
    date    : 09/03/24
*)
"numlib" libload.

(*
    If the file does not exist, the result is 1.
    If the file is empty, the result is also 1.
    If the file is smaller than 100, then the fseek needs to be adjusted.
    Otherwise, the result is one larger than the latest number.
*)
HIDE

init-file == "journal.txt" "r" fopen [] [] [pop 1] ifte;
is-filled == 0 2 fseek pop ftell;
get-seqnr ==
is-filled		# size
100 -			# 100 before end
[negative] [pop 0]	# not before start
[] ifte			# start of file
0 fseek pop		# fp
fgets			# fp old-line
swap			# old-line fp
[over null not]		# old-line fp empty?
[fgets			# old-line fp new-line
[null not]		# old-line fp new-line empty?
[rotate			# new-line fp old-line
pop]			# new-line fp
[swap]			# old-line "" fp
ifte]
while			# line "" fp
fclose			# line ""
pop			# line
10 strtol		# last known seqnr
succ			# incremented by 1

IN

next-seqnr ==
init-file		# fp or 1
[] [[is-filled] [get-seqnr] [pop 1] ifte] ifinteger.

(* unit test *)
# next-seqnr.
