(*
    module  : header.joy
    version : 1.7
    date    : 09/25/24
*)
"screen.joy" include.
"getkey.joy" include.

(*
    This prints the header of a page, as well as the introduction screen.
    The text of the header consists of 3 parts: program, title, company.
    The title needs to be centered between program on the left and company
    on the right.
*)
LIBRA

(* build a string of space characters *)
space-string ==		# number
'\032 ""		# number char string
rolldown		# char string number
[dupd cons] times popd;

(*
    Similar to PrintHead, except that the x-position of the header is given as
    parameter. The program name can be printed at 3; the company name at Cols
    minus length company name minus 2.

    Print has this signature:
    str x y effect

    The spaces around the title must cover the entire space between the program
    name and the company name.
*)
PrintHeadAtX ==		# x-pos title
swap			# title x-pos
Cols 4 -		# title x-pos avail
PROGRAM-NAME size	# title x-pos avail size
COMPANY-NAME size	# title x-pos avail size size
+			# title x-pos avail used
-			# title x-pos remain
space-string		# title x-pos spaces
PROGRAM-NAME size	# title x-pos spaces prog
3 +			# title x-pos spaces start
3			# title x-pos spaces start y-pos 
false			# title x-pos spaces start y-pos effect
print			# title x-pos
3			# title x-pos y-pos
false			# title x-pos y-pos effect
print			#
Cols			# number of columns on the screen
pred			# one space character after company name
COMPANY-NAME size -	# subtract the space needed for the company name
COMPANY-NAME		# x-pos company
swap			# company x-pos
3			# company x-pos y-pos
false			# company x-pos y-pos false
print			#
PROGRAM-NAME		# program
3			# program x-pos
dup			# program x-pos y-pos
false			# program x-pos y-pos effect
print			#
1 6 gotoxy clreod;	# Clear the rest of the screen.

(*
    If the title needs to be centered, the x-position should be calculated
    first.
*)
calc-xpos ==		# title
Cols 4 -		# title width, subtracting borders
over size -		# title width, subtracting title size
PROGRAM-NAME size -	# title width, subtracting program size
COMPANY-NAME size -	# title width, subtracting company size
2 /			# title spaces, splitting remaining space
3 +			# title spaces, adding border
PROGRAM-NAME size +;	# title x-pos, adding program size

PrintHead ==		# title
calc-xpos		# title x-pos
swap			# x-pos title
PrintHeadAtX;		#

PrintBox ==
# 1 4 gotoxy clreoln
1 1 Cols pred 5 printbox;

PrintHeader ==		# title
clrscr
PrintBox
PrintHead;

Intro ==
# 25 80			# test key.txt
(* Install a number of global variables *)
[Cols] assign pred [Rows] assign

(* Start the introduction screen *)
INTRO-MENU PrintHeader	# lines 1-5
			# lines 6-12
ACCOUNTING-MADE-EASY	# text
Cols over size - 2 /	# calculate x-position
13 false print		# print on line 13
VERSION " 1.1" concat	# text
Cols over size - 2 /	# calculate x-position
15 false print		# print on line 15
"Copyright Saru Janpu 2020"
Cols over size - 2 /	# calculate x-position
17 false print		# print on line 17
			# lines 18-24
PRESS-TO-CONTINUE 1 25 false print
getkey [RARR !=] [pop getkey] while pop.	# remove RARR
