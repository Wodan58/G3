(*
    module  : limits.joy
    version : 1.2
    date    : 05/05/24
*)

LIBRA 

(*
    Input of a key at x, y. The string will have a size of 1 and there is a
    limited choice. String and choice are presented separately, allowing a
    space character to be presented, even if a space is not one of the limited
    characters that are allowed. Suppose the choice is between y or n, then the
    space means that no choice was made.
*)
limits ==		# ok str x y
rolldown		# ok x y str
dup			# ok x y str str
"[" swoncat		# ok x y str "[str"
"]" concat		# ok x y str "[str]"
3 pick			# ok x y str "[str]" x
pred			# ok x y str "[str]" x-1
3 pick			# ok x y str "[str]" x-1 y
true			# ok x y str "[str]" x-1 y true
print			# ok x y str
edit-one		# ok x y str ch
swap			# ok x y ch str
rollupd			# ok ch x y str
dup			# ok ch x y str str
rollupd			# ok ch str x y str
" " swoncat		# ok ch str x y " str"
" " concat		# ok ch str x y " str "
rollup			# ok ch str str x y
[pred] dip		# ok ch str str x-1 y
false			# ok ch str str x-1 y false
print			# ok ch str
rotate			# str ch ok
pop.			# str ch
