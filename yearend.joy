(*
    module  : yearend.joy
    version : 1.4
    date    : 06/29/24
*)

(*
    Read accounts, filter profit&loss accounts, remember only account number.
*)
LIBRA

yearend ==

dup first second [BegDate] assign
dup second second [EndDate] assign
dup third second [Result] assign

init-Acc Acc
[second "w" =] filter
[first] map [Profit] assign

(*
    Read postings.
*)
init-Jrn Jrn
(*
    Filter postings with date between start and end.
*)
[third dup BegDate >= swap EndDate <= and] filter
(*
    Filter postings with a profit&loss account.
*)
[second Profit in] filter
(*
    Limit the postings to account and amount.
*)
[dup second swap fourth pairlist] map
(*
    Add an amount to each profit&loss account.
*)
Profit [["" 0] cons] map
(*
    Summarize each account separately.
*)
swap summer
(*
    Filter accounts with zero amounts.
*)
[third null not] filter		# [accnt "" amnt]
(*
    Extract the third field and summarize it.
*)
dup [third] map sum		# [accnt "" amnt] sum
(*
    Get a new posting number.
*)
next-seqnr			# [accnt "" amnt] sum seqnr
(*
    Post the summary first.
*)
dup				# [accnt "" amnt] sum seqnr seqnr
[yearfinish] dip		# [accnt "" amnt] seqnr
swap				# seqnr [accnt "" amnt]
(*
    Print while negating the amounts.
*)
[over yearendpost] step pop;	# remove seqnr

(*
    First check that dates and account are filled. If not, don't do anything.
*)
do-yearend ==
[yeartest]			# test everything is filled
[yearend]
[]
ifte.
