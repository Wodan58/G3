(*
    module  : palview.joy
    version : 1.6
    date    : 06/28/24
*)
LIBRA

init-pal-bal-view ==
read-filter
init-Acc Acc;

mid-pal-bal-view ==
[dup first swap fourth 0 pairlist cons] map

(*
    Read postings and make the list smaller
*)
init-Jrn Jrn
apply-filter
[dup second swap fourth pairlist] map

(*
    Sum the amounts
*)
summer

(*
    Remove accounts with zero amounts.
*)
[third null not] filter

dup

(*
    Calculate the total.
*)
[third] map sum

swap

(*
    Format the list to a list of strings.
*)
[dup first num2str swap dup second dup size 30 - neg 
"" swap			# build a string the size of the remaining space
['\032 swons]
times			# this really should be a separate definition
concat
swap third format-num right-align
'\t swons concat '\t swons concat]
map
["\t\t\t\t\t--------------------------"] concat
swap;

end-pal-bal-view ==
[format-num right-align
"\t" NET-LOSS "\t\t\t" concat concat swoncat]
[format-num right-align
"\t" NET-PROFIT "\t\t\t" concat concat swoncat]
ifte
unitlist
concat;

(*
    Profit & Loss
*)
Pal-view ==
init-pal-bal-view
[second "w" =] filter
mid-pal-bal-view
[positive]
end-pal-bal-view
["" ""] PROFIT-LOSS-SCREEN
rest-report.
