(*
    module  : wordin.joy
    version : 1.2
    date    : 06/27/24
*)

LIBRA 

(*
    Words are positive.
*)
num2str ==		# account
'd 0 0 format;		# string

(*
    Input of an unsigned integer at x, y. The integer needs to be converted to
    a string prior to input and converted to integer after input.
*)
wordin ==		# amount x y
rolldown		# x y amount
'0			# x y amount '0
swap			# x y ok amount
rollupd			# ok x y amount
'd -19 1 format		# max. width is 19 characters; min. width is 1
dup			# ok x y "amount" "amount"
"[" swoncat		# ok x y "amount" "[amount"
"]" concat		# ok x y "amount" "[amount]"
3 pick			# ok x y "amount" "[amount]" x
pred			# ok x y "amount" "[amount]" x-1
3 pick			# ok x y "amount" "[amount]" x-1 y
true			# ok x y "amount" "[amount]" x-1 y true
print			# ok x y "amount"
0			# ok x y "amount" 0
editln			# ok x y str pos ch
popd			# ok x y str ch -> pos is no longer needed
swap			# ok x y ch str
rollupd			# ok ch x y str
10 strtol		# ok ch x y amount
dup			# ok ch x y amount amount
'd -19 1 format		# ok ch x y amount format
rollupd			# ok ch amount x y format
" " swoncat		# ok ch amount x y " format"
" " concat		# ok ch amount x y " format  "
rollup			# ok ch amount format x y
[pred] dip		# ok ch amount format x-1 y
false			# ok ch amount format x-1 y false
print			# ok ch amount
rotate			# amount ch ok
pop.			# amount ch
