(*
    module  : screen.joy
    version : 1.5
    date    : 09/19/24
*)

(*
    This code operates under a number of assumptions:
    The terminal is in raw mode: translation of \n to \r\n;
    The size of the terminal is fixed at 25 lines and 80 columns;
    Printing at the right edge does not move the cursor;
    Graphical characters can be enabled and disabled;
    All other escape sequences are also accepted.
*)

LIBRA
(* x, y; upper left is at (1,1) *)
gotoxy == "\027[" putchars 'd 0 0 format putchars '; putch 'd 0 0 format
	  putchars 'H putch;
cursave == "\0277" putchars;
currestore == "\0278" putchars;
curhome == "\027[H" putchars;
curstart == '\r putch;
curup == "\027[A" putchars;
curdown == '\n putch;
curleft == '\b putch;
curright == "\027[C" putchars;
clreod == "\027[J" putchars;
clrscr == curhome clreod;
clreoln == "\027[K" putchars;
inschr == "\027[1@" putchars;
delchr == "\027[1P" putchars;
colwhite == "\027[0m" putchars;
colred == "\027[1;31m" putchars;
colgreen == "\027[1;32m" putchars;
colyellow == "\027[1;33m" putchars;
colblue == "\027[1;34m" putchars;
colpurple == "\027[1;35m" putchars;
colcyan == "\027[1;36m" putchars;
graphenable == "\027(0" putchars;
graphdisable == "\027(B" putchars;

(* str x y effect *)
print == rollup gotoxy [] [colred] [] ifte swap putchars [] [colwhite] [] ifte
	 pop;

(* xh yh xl yl *)
horizontal ==

(* enable graphics *)
graphenable

over 4 pick -
['q putch] times

(* disable graphics *)
graphdisable;

(* xh yh xl yl *)
box ==

(* enable graphics *)
graphenable

(* upper left *)
'l putch

(* horizontal line: o,p,q,r,s *)
over pred ['q putch] times

(* upper right *)
'k putch

(* next line *)
'\n putch

(* vertical line *)
dup pred pred ["x\n" putchars] times

(* lower left *)
'm putch

(* horizontal line: o,p,q,r,s *)
over pred ['q putch] times

(* lower right *)
'j putch

(* vertical line from bottom to top *)
dup pred pred ["\027[Ax" putchars] times

(* disable graphics *)
graphdisable;

(* printbox - either print a box or only a horizontal line *)
(* xh yh xl yl *)
printbox ==
3 pick 3 pick gotoxy
[2 pick over =]
[horizontal] [box] ifte
pop pop pop pop;

(* print one character, with effect, at the current cursor position *)
echoone == colred putch colwhite;

(* print one character, with effect, at (x,y) without disturbing the cursor *)
(* ch x y *)
printone == cursave gotoxy colred putch colwhite currestore;

(* print two characters, with effect, at (x,y) without disturbing the cursor *)
(* ch1 ch2 x y proc *)
printtwo == cursave [gotoxy] dip call colred swap putch putch colwhite
	    currestore;

(* print a string, with effect, at the current cursor position *)
printstring == colred putchars colwhite.

(* unit test *)
# clrscr
# 1 1 79 5 printbox.
